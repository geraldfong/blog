// declare logistics problem objects and actions as BLOG objects
type Load; type Unload; type Drive;
type Box;  type Truck;  type City;

// declare the individual objects
guaranteed Box b1, b2, b3;
guaranteed Truck t1, t2, t3;
guaranteed City c1, c2, c3;

random Boolean Load(Box, Truck, NaturalNum);
random Boolean Unload(Box, Truck, NaturalNum);
random Boolean Drive(City, Truck, NaturalNum);

random Boolean UniqueLoad(NaturalNum);
random Boolean UniqueUnload(NaturalNum);
random Boolean UniqueDrive(NaturalNum);

random Boolean BoxIn(Box, City, NaturalNum);
random Boolean TruckIn(City, Truck, NaturalNum);
random Boolean BoxOn(Box, Truck, NaturalNum);

Load (b, tr, t) {
  if (exists City c (BoxIn(b, c, t) & TruckIn(c, tr, t))) then
    ~ TabularCPD[[0.9]]()
    //= false
  else
    = false
};

Unload (b, tr, t) {
  ~ TabularCPD[[0.9]]()
  //= false
};

Drive (c, tr, t) {
  ~ TabularCPD[[0.9]]()
  //= false
};

UniqueLoad(t) {
  //= (forall NaturalNum t forall Truck tr forall Box b forall Truck tr2 forall Box b2
  //    (Load(b, tr, t) & Load(b2, tr2, t) -> tr = tr2 & b = b2))
  = (forall Truck tr forall Box b forall Truck tr2 forall Box b2
      (Load(b, tr, t) & Load(b2, tr2, t) -> tr = tr2 & b = b2))
};

UniqueUnload(t) {
  //= (forall NaturalNum t forall Box b forall Truck tr forall Box b2 forall Truck tr2
  //    (Unload(b, tr, t) & Unload(b2, tr2, t) -> b = b2 & tr = tr2))
  = (forall Box b forall Truck tr forall Box b2 forall Truck tr2
      (Unload(b, tr, t) & Unload(b2, tr2, t) -> b = b2 & tr = tr2))
};

UniqueDrive(t) {
  //= (forall NaturalNum t forall  City c forall  Truck tr forall  City c2 forall  Truck tr2
  //    (Drive(c, tr, t) & Drive(c2, tr2, t) -> c = c2 & tr = tr2))
  = (forall  City c forall  Truck tr forall  City c2 forall  Truck tr2
      (Drive(c, tr, t) & Drive(c2, tr2, t) -> c = c2 & tr = tr2))
};

BoxIn(b, c, t) {
  if t = 0 & c = c1 then
    = true
  elseif t = 0 & c != c1 then
    = false
  else
    = (exists Truck tr (Unload(b, tr, Pred(t)) & BoxOn(b, tr, Pred(t)) & TruckIn (c, tr, Pred(t))))
      | BoxIn(b, c, Pred(t))
      & !(exists Truck tr (Load(b, tr, Pred(t)) & BoxIn(b, c, Pred(t)) & TruckIn(c, tr, Pred(t))))
};

TruckIn(c, tr, t) {
  if t = 0 & c = c1 then
    = true
  elseif t = 0 & c != c1 then
    = false
  else
    = Drive(c, tr, Pred(t))
      | TruckIn(c, tr, Pred(t))
      & !(exists City c2 (Drive(c, tr, Pred(t)) & c2 != c))
};

BoxOn(b, tr, t) {
  if t = 0 then
    = false
  else
    = (exists City c (Load(b, tr, Pred(t)) & BoxIn(b, c, Pred(t)) & TruckIn(c, tr, Pred(t))))
      | BoxOn(b, tr, Pred(t))
      & !(exists City c (Unload(b, tr, Pred(t)) & BoxOn(b, tr, Pred(t)) & TruckIn(c, tr, Pred(t))))
};

